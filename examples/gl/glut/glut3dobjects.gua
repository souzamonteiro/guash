#!/usr/local/bin/glutguash

delta_x = 0.0;
delta_y = 0.0;
mouse_x = 0;
mouse_y = 0;

solid = TRUE;
object = "Teapot";

#
# Material and light initializations.
#
function init() {
    diffuse_material = glArrayOfFloat([0.5, 0.5, 0.5, 1.0]);
    mat_specular = glArrayOfFloat([1.0, 1.0, 1.0, 1.0]);
    light_position = glArrayOfFloat([1.0, 1.0, 1.0, 0.0]);
    
    glClearColor(0.0, 0.0, 0.0, 0.0);
	
    glShadeModel(GL_SMOOTH);
    glEnable(GL_DEPTH_TEST);
    glMaterial(GL_FRONT, GL_DIFFUSE, diffuse_material);
    glMaterial(GL_FRONT, GL_SPECULAR, mat_specular);
    glMaterial(GL_FRONT, GL_SHININESS, 25.0);
    glColorMaterial(GL_FRONT, GL_DIFFUSE);
    glEnable(GL_COLOR_MATERIAL);
	glLight(GL_LIGHT0, GL_POSITION, light_position);
    glEnable(GL_LIGHTING);
    glEnable(GL_LIGHT0);
    
    glFree(diffuse_material);
    glFree(mat_specular);
    glFree(light_position);
}

#
# Display callback.
#
function display() {
    glClear(GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT);

    # Draw the cube...
    glRotate($delta_x, 1.0, 0.0, 0.0);
	glRotate($delta_y, 0.0, 1.0, 0.0);
	
	glColor(1.0, 0.0, 0.0, 1.0);

    if ($solid) {
        if ($object == "Cone") {
   	        glutSolidCone(0.5, 0.5, 20, 20);
        } elseif ($object == "Cube") {
   	        glutSolidCube(0.5);
        } elseif ($object == "Dodecahedron") {
            glutSolidDodecahedron();
        } elseif ($object == "Icosahedron") {
   	        glutSolidIcosahedron();
        } elseif ($object == "Octahedron") {
   	        glutSolidOctahedron();
        } elseif ($object == "Sphere") {
   	        glutSolidSphere(0.5, 20, 20);
        } elseif ($object == "Teapot") {
   	        glutSolidTeapot(0.5);
        } elseif ($object == "Tetrahedron") {
   	        glutSolidTetrahedron();
        } elseif ($object == "Torus") {
   	        glutSolidTorus(0.3, 0.5, 20, 20);
        }
	} else {
        if ($object == "Cone") {
   	        glutWireCone(0.5, 0.5, 20, 20);
        } elseif ($object == "Cube") {
   	        glutWireCube(0.5);
        } elseif ($object == "Dodecahedron") {
   	        glutWireDodecahedron();
        } elseif ($object == "Icosahedron") {
   	        glutWireIcosahedron();
        } elseif ($object == "Octahedron") {
   	        glutWireOctahedron();
        } elseif ($object == "Sphere") {
   	        glutWireSphere(0.5, 20, 20);
        } elseif ($object == "Teapot") {
   	        glutWireTeapot(0.5);
        } elseif ($object == "Tetrahedron") {
   	        glutWireTetrahedron();
        } elseif ($object == "Torus") {
   	        glutWireTorus(0.3, 0.5, 20, 20);
        }
    }
    glFlush();
}

#
# Reshape callback.
#
function reshape(width, height) {
	glViewport(0, 0, width, height);
	glMatrixMode(GL_PROJECTION);
	glLoadIdentity();
	if (width <= height) {
		glOrtho(-1.5, 1.5, -1.5 * height / width, 1.5 * height / width, -10.0, 10.0);
	} else {
		glOrtho(-1.5 * width / height, 1.5 * width / height, -1.5, 1.5, -10.0, 10.0);
    }
	glMatrixMode(GL_MODELVIEW);
	glLoadIdentity();
}

#
# Keyboard callback.
#
function keyboard(key, x, y) {
    if (sprintf("%c", key) == "a") {
        $object = "Cone";
    } elseif (sprintf("%c", key) == "b") {
        $object = "Cube";
    } elseif (sprintf("%c", key) == "c") {
        $object = "Dodecahedron";
    } elseif (sprintf("%c", key) == "d") {
        $object = "Icosahedron";
    } elseif (sprintf("%c", key) == "e") {
        $object = "Octahedron";
    } elseif (sprintf("%c", key) == "f") {
        $object = "Sphere";
    } elseif (sprintf("%c", key) == "g") {
        $object = "Teapot";
    } elseif (sprintf("%c", key) == "h") {
        $object = "Tetrahedron";
    } elseif (sprintf("%c", key) == "i") {
        $object = "Torus";
    } elseif (sprintf("%c", key) == "s") {
        $solid = TRUE;
    } elseif (sprintf("%c", key) == "w") {
        $solid = FALSE;
    } elseif (key == 27) {
        glutDestroyWindow($win);
        exit(0);
    }

    glutPostRedisplay();
}

#
# Mouse callback.
#
function mouse(button, state, x, y) {
    glutPostRedisplay();
}

#
# Mouse motion callback.
#
function motion(x, y) {
    $delta_x = ($mouse_x - x) * 1.0;
    $delta_y = ($mouse_y - y) * 1.0;
    
    $mouse_x = x;
    $mouse_y = y;

    glutPostRedisplay();
}

glutInitDisplayMode(GLUT_SINGLE | GLUT_RGB | GLUT_DEPTH);
glutInitWindowSize(500, 500);
glutInitWindowPosition(100, 100);
win = glutCreateWindow("GLUT Tea Pot");

init();

glutReshapeFunc("reshape");
glutDisplayFunc("display");
glutKeyboardFunc("keyboard");
glutMouseFunc("mouse");
glutMotionFunc("motion");

glutMainLoop();

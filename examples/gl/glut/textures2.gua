#!/usr/local/bin/glutguash

texture1 = ""
texture_width = 0
texture_height = 0
texture_names = ""

function create_texture_images() {
    texture = gloLoadPPM($texture_file, "texture_width", "texture_height");
    
    $texture1 = glArrayOfByte(texture);
    
    #gloSavePPM(texture, "/tmp/texture.ppm", $texture_width, $texture_height);
}

function display() {
    glClear(GL_COLOR_BUFFER_BIT);
    glClear(GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT);
    
    glBindTexture(GL_TEXTURE_2D, $texture_names[0]);
    
    glBegin(GL_QUADS);
        glTexCoord(0.0, 0.0); glVertex(-0.5, 0.5, 0.0);
        glTexCoord(1.0, 0.0); glVertex(0.5, 0.5, 0.0);
        glTexCoord(1.0, 1.0); glVertex(0.5, -0.5, 0.0);
        glTexCoord(0.0, 1.0); glVertex(-0.5, -0.5, 0.0);
    glEnd();
    
    glFlush();
}

function keyboard(key, x, y){
    if (sprintf("%c", key) == "q") {
        glutDestroyWindow($win);
        exit(0);
    }
}

function init() {
    glClearColor(0.0, 0.0, 0.0, 0.0);
    
    glShadeModel(GL_FLAT);
    glEnable(GL_DEPTH_TEST);
    
    create_texture_images();

    glPixelStore(GL_UNPACK_ALIGNMENT, 1);
    
    $texture_names = glGenTextures(1);
    
    glBindTexture(GL_TEXTURE_2D, $texture_names[0]);
    glTexParameter(GL_TEXTURE_2D, GL_TEXTURE_WRAP_S, GL_REPEAT);
    glTexParameter(GL_TEXTURE_2D, GL_TEXTURE_WRAP_T, GL_REPEAT);
    glTexParameter(GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_LINEAR);
    glTexParameter(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_LINEAR);
    glTexEnv(GL_TEXTURE_ENV, GL_TEXTURE_ENV_MODE, GL_MODULATE);
    glTexImage2D(GL_TEXTURE_2D, 0, GL_RGB, $texture_width, $texture_height, 0, GL_RGB, GL_UNSIGNED_BYTE, $texture1);
    
    glEnable(GL_TEXTURE_2D);
}

function finish() {
    $texture1 = glFree($texture1);
}

# Get the textures path...
script_file = fsFullPath($argv[1])
script_path = fsPath(script_file)
texture_file = script_path + "/images/13071980385.ppm"

# Create the main window...
glutInitDisplayMode(GLUT_SINGLE | GLUT_RGB | GLUT_DEPTH);
glutInitWindowSize(500, 500);
glutInitWindowPosition(100, 100);
win = glutCreateWindow("OpenGL Textures");

init();

glutDisplayFunc("display");
glutKeyboardFunc("keyboard");

glutMainLoop();

finish();

